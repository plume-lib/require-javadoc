// REQUIREMENTS to make a release:
//  * Perform work on a machine connected to the CSE filesystem.
//  * Your ~/.gradle/gradle.properties file must contain:
//     ossrhUsername=YOUR_USER_NAME_HERE
//     ossrhPassword=YOUR_PASSWORD_HERE

// To make a release:
//  * git pull
//  * Update the version number in ../README.md and in this file (thrice).
//  * Update ../CHANGELOG.md .
//  * Run in the top-level directory:  ./gradlew javadocWeb && ./gradlew clean uploadArchives
//  * Browse to https://oss.sonatype.org/#stagingRepositories, complete the Maven Central release.
//  * Stage changes.
//  * Add a git tag:
//    VER=0.3.0 && git commit -m "Version $VER" && git push && git tag -a v$VER -m "Version $VER" && git push && git push --tags
//  * Make a GitHub release. Go to the GitHub releases page, make a release, call it "require-javadoc 0.3.0", attach the .jar file from ../build/libs/

///////////////////////////////////////////////////////////////////////////
/// Maven Central publishing
/// (From http://central.sonatype.org/pages/gradle.html )

apply plugin: 'maven'
apply plugin: 'signing'

group = "org.plumelib"
archivesBaseName = "require-javadoc"
version = "0.3.0"

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { project.gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
  doFirst {
    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        pom.project {
          name 'Require-Javadoc'
          packaging 'jar'
          description 'Require Javadoc comments to be present.'
          url 'https://github.com/plume-lib/require-javadoc'

          scm {
            connection 'scm:git:git@github.com:plume-lib/require-javadoc.git'
            developerConnection 'scm:git:git@github.com:plume-lib/require-javadoc.git'
            url 'git@github.com:plume-lib/require-javadoc.git'
          }

          licenses {
            license {
              name 'MIT License'
              url 'https://opensource.org/licenses/MIT'
            }
          }

          developers {
            developer {
              id 'mernst'
              name 'Michael Ernst'
              email 'mernst@alum.mit.edu'
            }
          }
        }
      }
    }
  }
}
